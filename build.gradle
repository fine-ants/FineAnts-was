plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.11'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id "me.champeau.jmh" version "0.7.1"
}

group = 'co.fineants'
version = '0.0.27'

java {
    sourceCompatibility = '17'

    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly.extendsFrom annotationProcessor
    configureEach {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    asciidoctorExt
}

repositories {
    mavenCentral()
}

dependencies {
    // spring
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // jwt 라이브러리
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // JWT 서명 검증 라이브러리
    implementation 'com.auth0:java-jwt:3.19.0'
    implementation 'com.auth0:jwks-rsa:0.21.1'

    // AWS S3
    implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'

    //encoder 추가
    implementation 'org.springframework.security:spring-security-crypto'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // mockWebserver
    testImplementation 'com.squareup.okhttp3:okhttp:4.12.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'

    // firebase
    implementation 'com.google.firebase:firebase-admin:9.1.1'

    runtimeOnly 'com.mysql:mysql-connector-j'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    // final class mocking
    testImplementation 'org.mockito:mockito-inline:2.13.0'

    // test container
    testImplementation 'org.testcontainers:junit-jupiter:1.19.7'
    testImplementation 'org.testcontainers:mysql:1.19.7'
    testImplementation 'org.testcontainers:localstack:1.19.7'
    testImplementation 'io.aiven:testcontainers-fake-gcs-server:0.1.0'

    // Rest Docs
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    // RxJava
    implementation 'io.reactivex.rxjava3:rxjava:3.1.8'

    // Rest-Assured
    testImplementation 'io.rest-assured:rest-assured'

    // Csv Reader
    implementation 'org.apache.commons:commons-csv:1.11.0'

    // QueryDsl
    implementation "com.querydsl:querydsl-jpa:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // JMH
    testImplementation 'org.openjdk.jmh:jmh-core:1.37'
    testImplementation 'org.openjdk.jmh:jmh-generator-annprocess:1.37'

    // Prometheus
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // ShedLock
    implementation 'net.javacrumbs.shedlock:shedlock-spring:6.3.1'
    implementation 'net.javacrumbs.shedlock:shedlock-provider-redis-spring:6.3.1'

    // Database migration
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'

    // Google Cloud Storage
    implementation 'com.google.cloud:google-cloud-storage:2.56.0'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('copyPrivate', Copy) {
    copy {
        from './secret'
        include "*.yml"
        into 'src/main/resources/secret'
    }
    copy {
        from './secret'
        include "*.json"
        into 'src/main/resources/secret/firebase'
    }
    copy {
        from './secret/gcp'
        include "*.json"
        into 'src/main/resources/secret/gcp'
    }
}

// Rest Docs configuration start
ext { // 전역변수
    snippetsDir = file('build/generated-snippets') as File
}

tasks.register('restDocsTest', Test) {
    description = "Runs only tests tagged with 'rest-docs' for documentation."
    group = "verification"

    // test task의 설정을 복사함
    testClassesDirs = tasks.named('test').get().testClassesDirs
    classpath = tasks.named('test').get().classpath

    // Junit Platform 설정 유지
    useJUnitPlatform {
        // 'rest-docs' 태그가 붙은 테스트만 실행
        includeTags 'rest-docs'
    }

    // 기존 test task처럼 스니펫 디렉토리를 출력으로 지정
    outputs.dir snippetsDir
}

test {
    // rest-docs with tag exclude
    useJUnitPlatform {
        excludeTags 'rest-docs'
    }
}

asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'

    sources { // 특정 파일만 html로 만든다
        include("**/index.adoc")
    }
    baseDirFollowsSourceFile() // 다른 adoc 파일을 include 할때 경로를 baseDir로 맞춘다
    dependsOn tasks.named("restDocsTest") // restDocsTest task 이후에 실행되도록 설정
}
// Rest Docs configuration end

bootJar {
    // Rest Docs
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}") {
        into 'static/docs'
    }

    archiveFileName = "fineAnts_app.jar"
    copyPrivate
}

// QueryDSL configuration start
def generated = "src/main/generated"

//Querydsl Q Class 생성 위치 지정
tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(file(generated))
}

//java source set 에 Querydsl Q Class 위치 추가
sourceSets {
    main.java.srcDirs += [generated]
}

//gradle clean 시, Q Class 디렉토리까지 삭제하도록 설정
clean {
    delete file(generated)
}
// QueryDSL configuration end
